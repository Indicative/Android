apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}


android {
    compileSdkVersion 23
    buildToolsVersion '30.0.2'
}

version = "1.1.0"
group = "com.indicative.client.android"

def isSnapshot() {
    return version.endsWith("SNAPSHOT")
}

if (!isSnapshot()) {
	signing {
    	required { gradle.taskGraph.hasTask("uploadArchives") }
    	sign configurations.archives
	}
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      
      	if (isSnapshot()) {
      		repository(url:'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath)
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
      	} else {
        	repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
        		authentication(userName: sonatypeUsername, password: sonatypePassword)
        	}
        }
        
        pom.project {
            name 'Indicative-Android'
            packaging 'aar'
            description 'Android client for Indicative\'s REST API'
            url 'https://github.com/Indicative/Indicative-Android'

            scm {
                url 'https://github.com/Indicative/Indicative-Android'
                connection 'scm:git:git://github.com/Indicative/Indicative-Android.git'
                developerConnection 'scm:git:https://bealeindicative@github.com/Indicative/Indicative-Android.git'
            }

            licenses {
                license {
                    name 'Simplified BSD License'
                    url 'https://github.com/Indicative/Indicative-Android/blob/master/LICENSE.txt'
                    distribution 'repo'
                    comments '2-clause BSD License, aka the FreeBSD License. See LICENSE.txt'
                }
            }

            developers {
                developer {
                    id 'andrewbeale'
                    name 'Andrew Beale'
                    email 'andrew.beale@indicative.com'
                }
                developer {
                    id 'jackiebenowitz'
                    name 'Jackie Benowitz'
                    email 'jackie@indicative.com'
                }
            }
        }
    }
}